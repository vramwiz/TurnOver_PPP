unit Shake_PPP_FilterPlugin;

interface

uses
  Winapi.Windows,
  AviUtl2FilterTypes;

function InitializeShakePlugin(Version: DWORD): Byte;
procedure FinalizeShakePlugin;
function GetShakeFilterTable: PFILTER_PLUGIN_TABLE;

implementation

uses
  System.SysUtils,
  System.UITypes,
  Vcl.Dialogs,
  Vcl.Forms,
  Shake_PPP_DebugLog,
  Shake_PPP_FilterSettings,
  Shake_PPP_LastFrameCapture,
  Shake_PPP_RuntimeDeformer,
  Shake_PPP_SettingsForm,
  PluginFilterTable;

const
  FILTER_EFFECT_NAME = 'パンチラ';
  CURVE_DATA_ITEM_NAME = '布範囲データ';

var
  CurveDataItem: TFILTER_ITEM_STRING;
{$IFDEF DEBUG}
  LastRuntimeInputLogTick: UInt64;
{$ENDIF}

procedure DebugLogRuntimeInput(const Settings: TTurnOverRuntimeSettings);
{$IFDEF DEBUG}
var
  CurrentTick: UInt64;
{$ENDIF}
begin
{$IFDEF DEBUG}
  CurrentTick := GetTickCount64;
  if (LastRuntimeInputLogTick <> 0) and
    (CurrentTick - LastRuntimeInputLogTick < 1000) then
    Exit;
  LastRuntimeInputLogTick := CurrentTick;
  DebugLog(Format(
    'Runtime grip offsets: point1=(%.1f,%.1f) point2=(%.1f,%.1f) wind=%.1f period=%.1f direction=%.1f turbulence=%.2f ripple=%.1f waves=%.1f radius=%.2f fold=%.2f lighting=%.2f backside=%.2f castShadow=%.2f.',
    [Settings.GripOffsets[0].X, Settings.GripOffsets[0].Y,
     Settings.GripOffsets[1].X, Settings.GripOffsets[1].Y,
     Settings.WindStrength, Settings.WindPeriod,
     Settings.WindDirectionDegrees, Settings.WindTurbulence,
     Settings.RippleStrength, Settings.RippleCount,
     Settings.InfluenceRadius, Settings.FoldStrength,
     Settings.LightingStrength,
     Settings.BacksideStrength, Settings.CastShadowStrength]));
{$ENDIF}
end;

function EmptyProcVideo(Video: PFILTER_PROC_VIDEO): Byte; cdecl;
var
  CurveDataText: string;
  RuntimeSettings: TTurnOverRuntimeSettings;
begin
  try
    CaptureLastFrame(Video);
    CurveDataText := '';
    if Assigned(CurveDataItem.Value) then
      CurveDataText := string(CurveDataItem.Value);
    RuntimeSettings := CurrentTurnOverRuntimeSettings;
    DebugLogRuntimeInput(RuntimeSettings);
    ApplyRuntimeDeformation(Video, CurveDataText, RuntimeSettings);
  except
    on E: Exception do
      DebugLog('Video callback failed: ' + E.ClassName + ': ' + E.Message);
  end;
  Result := 1;
end;

procedure SettingsButtonCallback(Edit: PEDIT_SECTION); cdecl;
var
  BackgroundHeight: Integer;
  BackgroundPixels: TBytes;
  BackgroundStatus: string;
  BackgroundWidth: Integer;
  CurrentDataText: string;
  CurveDataError: string;
  SettingsForm: TFormShakeSettings;
  CopySucceeded: Boolean;
  FocusObject: OBJECT_HANDLE;
  SelectedDataText: string;
  Utf8DataText: UTF8String;
begin
  try
    DebugLog('Settings button clicked.');
    SettingsForm := TFormShakeSettings.Create(nil);
    try
      CopySucceeded := CopyLastFrame(BackgroundPixels, BackgroundWidth,
        BackgroundHeight, BackgroundStatus);
      DebugLog(Format('Settings frame copy: success=%s size=%dx%d status="%s".',
        [BoolToStr(CopySucceeded, True), BackgroundWidth,
         BackgroundHeight, BackgroundStatus]));
      if CopySucceeded then
        SettingsForm.SetBackgroundRgba(BackgroundPixels,
          BackgroundWidth, BackgroundHeight);
      SettingsForm.SetCaptureStatus(BackgroundStatus);
      CurrentDataText := '';
      if Assigned(CurveDataItem.Value) then
        CurrentDataText := string(CurveDataItem.Value);
      if not SettingsForm.TryLoadCurveDataText(CurrentDataText,
        CurveDataError) then
        MessageDlg('保存済みの曲線データを読み込めませんでした。' +
          sLineBreak + CurveDataError, mtWarning, [mbOK], 0);
      SettingsForm.ShowModal;
      if not SettingsForm.TrySaveCurveDataText(SelectedDataText,
        CurveDataError) then
      begin
        MessageDlg('曲線データを保存できませんでした。' + sLineBreak +
          CurveDataError, mtError, [mbOK], 0);
        Exit;
      end;
      if SelectedDataText = CurrentDataText then
        Exit;
      FocusObject := nil;
      if (Edit <> nil) and Assigned(Edit^.GetFocusObject) then
        FocusObject := Edit^.GetFocusObject();
      if (Edit = nil) or not Assigned(Edit^.SetObjectItemValue) or
        (FocusObject = nil) then
      begin
        MessageDlg('曲線データの保存対象を取得できませんでした。',
          mtError, [mbOK], 0);
        Exit;
      end;
      Utf8DataText := UTF8String(SelectedDataText);
      if not Edit^.SetObjectItemValue(FocusObject, FILTER_EFFECT_NAME,
        CURVE_DATA_ITEM_NAME, PAnsiChar(Utf8DataText)) then
      begin
        MessageDlg('曲線データを形状データ項目へ反映できませんでした。',
          mtError, [mbOK], 0);
        Exit;
      end;
      DebugLog(Format('Curve data saved: chars=%d.',
        [Length(SelectedDataText)]));
    finally
      SettingsForm.Free;
    end;
  except
    on E: Exception do
      MessageDlg('設定画面を開けませんでした。' + sLineBreak + E.Message,
        mtError, [mbOK], 0);
  end;
end;

var
  SettingsButton: TFILTER_ITEM_BUTTON;

function InitializeShakePlugin(Version: DWORD): Byte;
begin
  ResetDebugLog;
{$IFDEF DEBUG}
  LastRuntimeInputLogTick := 0;
{$ENDIF}
  DebugLog(Format('InitializePlugin version=%d.', [Version]));
  InitializeLastFrameCapture;
  InitializeRuntimeDeformer;
  Result := 1;
end;

procedure FinalizeShakePlugin;
begin
  DebugLog('UninitializePlugin started.');
  FinalizeRuntimeDeformer;
  FinalizeLastFrameCapture;
end;

function GetShakeFilterTable: PFILTER_PLUGIN_TABLE;
begin
  if GTable.Name = nil then
  begin
    AddButton(SettingsButton, '設定', SettingsButtonCallback);
    AddTurnOverFilterItems;
    AddString(CurveDataItem, CURVE_DATA_ITEM_NAME, '');
    SetupPluginTable(FILTER_FLAG_VIDEO or FILTER_FLAG_FILTER,
      FILTER_EFFECT_NAME, 'SYNC', '布を2点でつまんでめくるフィルタープラグイン',
      EmptyProcVideo, nil);
  end;
  Result := @GTable;
end;

end.
